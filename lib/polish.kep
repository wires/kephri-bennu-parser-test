package(
    prog,
    evaluate)
with
    import 'bennu::parse' {
            always,
            bind,
            binds,
            choice,
            attempt,
            either,
            eof,
            enumeration,
            expected,
            many,
            many1,
            next,
            rec,
            run
    },
    import 'bennu::text' {
            character, digit, letter, space
    },
    import 'bennu::lang' {
            between, then
    },
    import 'nu-stream::stream' {foldl}
in {

var join = foldl @ ((+), '');

var beginSpace = many(space);

var token = \p -> then(p, many(space));

var plus = character <| '+';
var times = character <| '*';

var op = \x -> always({'operator': x});

var add = next(plus, op('coproduct'));
var mul = next(times, op('product'));
    
var operator = choice(add, mul);

var number = bind(
    many1(digit),
    join \> parseInt \> always
);

var label = bind(
    many1(letter),
    join \> (\x -> x) \> always
);

var expr = rec(\expr ->
    either(
        binds(
            enumeration(
                token <| operator |> (expected @ 'operator'),
                expr |> (expected @ 'expression'),
                expr |> (expected @ 'expression')),
            \op, a, b -> always({'operator': op, 'x': a, 'y': b})),
        
        token <| number |> (expected @ 'number')));


var tag = token <| label;

var colon = token <| character <| ':';
var equals = token <| character <| '=';

var header = binds(
    enumeration(tag, colon, tag),
    \name, _, t -> always({header: {name: name, type: t}})
);

var body = binds(
    enumeration(tag, equals, expr),
    \name, _, e -> always({body: {name: name, expr: e}})
);

var typedef = binds(
    enumeration(header, body),
    \h, b -> always({
        header: h,
        body: b
    })
);

prog = between(beginSpace, eof, typedef);

evaluate = run @ prog;

}